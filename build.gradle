buildscript {
	repositories {
		mavenCentral()
		 jcenter()
	}
	dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.6
compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

def javaExecutablesPath = new File(java6Home, 'bin')
def javaExecutables = [:].withDefault { execName ->
    def executable = new File(javaExecutablesPath, execName)
    assert executable.exists() : "There is no ${execName} executable in ${javaExecutablesPath}"
    executable
}
tasks.withType(AbstractCompile) {
    options.with {
        fork = true
        forkOptions.javaHome = file(java6Home)
    }
}
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

tasks.withType(Javadoc) {
    executable = javaExecutables.javadoc
}

tasks.withType(Test) {
    executable = javaExecutables.java
    systemProperty "file.encoding", "UTF-8"
}

tasks.withType(JavaExec) {
    executable = javaExecutables.java
}

repositories {
		maven { url 'https://jitpack.io' }
        mavenCentral()
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compile
    destinationDir = file("docs")
}

task compileOne (type: JavaCompile) {
    source = fileTree(dir: 'src', include: '**/*.java')
    classpath = files('build/classes/main')
    destinationDir = file('build/classes/main')
    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'
}

dependencies {
    compile files(fileTree(dir: 'lib', includes: ['*.jar']))
    compile group: 'com.beust', name: 'jcommander', version: '1.47'	
    compile group: 'log4j', name: 'log4j', version: '1.2.7'
}

jar {
    manifest {
        attributes 'Main-Class': 'com.viglet.turing.wem.TurWEMCommander'
    }
 }
 
 shadowJar {
   baseName = 'turing-wem'
   classifier = null
   version = 'all'
   exclude 'vgn-*.jar'
   exclude 'thirdparty-combined.jar'
     dependencies {
      exclude(dependency('log4j:log4j'))
   }
}

compileOne.options.compilerArgs = ["-sourcepath", "$projectDir/src/main/java"]
